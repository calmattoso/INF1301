// Script:    TesteListaDeturpa.script
// Autor:     Carlos Lima Mattoso
// Data:      1/dez/2013 ( modificação )
// Propósito: Testar o módulo lista genérica duplamente encadeada, fazendo deturpações e verificações
//            atingindo completeza de cobertura de aretas

== Inicializar valores
=declararparm   lista0  int 0
=declararparm   lista1  int 1
=declararparm   lista2  int 2
=declararparm   lista3  int 3
=declararparm   lista4  int 4
=declararparm   lista5  int 5
=declararparm   lista6  int 6
=declararparm   lista7  int 7
=declararparm   lista8  int 8
=declararparm   lista9  int 9
=declararparm   lista10  int 10
=declararparm   lista11  int 11
=declararparm   lista12  int 12
=declararparm   lista13  int 13
=declararparm   lista14  int 14

=declararparm   OK             int  0
=declararparm   ListaVazia     int  1
=declararparm   FimLista       int  2
=declararparm   NaoAchou       int  3
=declararparm   FaltouMemoria  int  4
=declararparm   ErroEstrutura  int  6

=declararparm   pNULL          int  0
=declararparm   pNaoNULL       int  1

=declararparm   DeturpaEliminaCorrente   int  1 
=declararparm   DeturpaPtProxNulo        int  2 
=declararparm   DeturpaPtAntNulo         int  3 
=declararparm   DeturpaPtProxLixo        int  4 
=declararparm   DeturpaPtAntLixo         int  5 
=declararparm   DeturpaRetiraCorrente    int  8 
=declararparm   DeturpaCorrenteNulo      int  9 
=declararparm   DeturpaOrigemNulo        int  10 
=declararparm   DeturpaCorrenteLixo      int  11 
=declararparm   DeturpaOrigemLixo        int  12 
=declararparm   DeturpaFimNulo           int  13 
=declararparm   DeturpaFimLixo           int  14 
=declararparm   DeturpaNumElem           int  15 
=declararparm   DeturpaPtCabecaNulo      int  16 
=declararparm   DeturpaPtCabecaLixo      int  17 
=declararparm   DeturpaEspacoLista       int  18 
=declararparm   DeturpaEspacoElemento    int  19 
=declararparm   DeturpaListaNulo         int  20 
=declararparm   DeturpaListaLixo         int  21 
=declararparm   DeturpaPtAntPtProx       int  22 
=declararparm   DeturpaPtProxPtAnt       int  23 

== Iniciar contagem
=inicializarcontadores  ""
=lercontadores          "..\\scripts\\ListaDeturpa"
=iniciarcontagem

// Primeiro testa completeza de cobertura de arestas do modulo
== Criar a lista 0
=criarlista      lista0   1

== Verificar ObterValor relativo a lista vazia
=obtervalorelem  lista0   50     pNULL

== Inserir elemento em lista vazia e tenta procurá-lo
=inselemantes    lista0   0   OK
=procurarvalor   lista0   0      OK
=obtervalorelem  lista0   0   pNaoNULL

== Inserir elemento após primeiro
=inselemapos     lista0   1   OK
=obtervalorelem  lista0   1   pNaoNULL

== Inserir elemento antes do último
=inselemantes    lista0   2   OK
=obtervalorelem  lista0   2   pNaoNULL

== Excluir elemento do meio
=obtervalorelem  lista0   2   pNaoNULL
=excluirelem     lista0             OK

== Excluir elemento do início
=obtervalorelem  lista0   0   pNaoNULL
=excluirelem     lista0             OK

== Excluir elemento final, único 
=obtervalorelem  lista0   1   pNaoNULL
=excluirelem     lista0             OK

== Ver se a lista ficou vazia
=obtervalorelem  lista0   50     pNULL

== Destruir a lista
=destruirlista   lista0

== Criar uma lista, tenta excluir e avancar
=criarlista      lista1   1
=excluirelem     lista1   ListaVazia
=avancarelem     lista1   0   ListaVazia

== Insere dois elementos na lista
=inselemapos     lista1   0   OK
=inselemapos     lista1   1   OK

== Esvaziar a lista e ver se ficou vazia
=esvaziarlista   lista1
=obtervalorelem  lista1   0     pNULL
=obternumelems   lista1   0

== Destruir a lista
=destruirlista   lista1

== Criar uma lista com quatro elementos
=criarlista      lista2   1
=inselemapos     lista2   0   OK
=inselemapos     lista2   1   OK
=inselemapos     lista2   2   OK
=inselemapos     lista2   3   OK

== Faz avanco nulo em lista
=avancarelem     lista2   0   OK

== Verificar tamanho da lista
=obternumelems   lista2   4

== Testar procura por elemento que nao esta na lista
=procurarvalor   lista2   1   NaoAchou


== Verificar a lista de trás para diante
=irfinal         lista2
=obtervalorelem  lista2   3   pNaoNULL
=avancarelem     lista2   -1     OK
=obtervalorelem  lista2   2   pNaoNULL
=avancarelem     lista2   -1     OK
=obtervalorelem  lista2   1   pNaoNULL
=avancarelem     lista2   -1     OK
=obtervalorelem  lista2   0   pNaoNULL
=avancarelem     lista2   -1     FimLista

== Verificar a lista de frente para trás
=obtervalorelem  lista2   0   pNaoNULL
=avancarelem     lista2   1      OK
=obtervalorelem  lista2   1   pNaoNULL
=avancarelem     lista2   1      OK
=obtervalorelem  lista2   2   pNaoNULL
=avancarelem     lista2   1      OK
=obtervalorelem  lista2   3   pNaoNULL
=avancarelem     lista2   1      FimLista

== Verifica numero de espaços alocados
=obternumeroespacosalocados    5

== Verificar a lista andando demais de trás para diante
=avancarelem     lista2   -10    FimLista
=obtervalorelem  lista2   0   pNaoNULL

== Verificar a lista andando demais de frente para trás
=avancarelem     lista2   10     FimLista
=obtervalorelem  lista2   3   pNaoNULL

== Testar ir pro inicio da lista
=irinicio        lista2 

== Testar ir pro fim da lista
=irfinal         lista2

== Avanca e insere entre dois elementos
=irinicio        lista2
=avancarelem     lista2   1  OK
=inselemapos     lista2   0  OK

== Testar lista com estrutura correta
=verificarlista lista2  OK

== Destruir a lista
=destruirlista   lista2

== Procura em lista vazia
=criarlista     lista3    1
=procurarvalor   lista3   0  ListaVazia

== Limita quantidade de memoria e tenta alocar elemento
=limitarmemoriadisponivel  1

== Tenta inserir com memoria limitada
=inselemapos   lista3  4  FaltouMemoria
=inselemantes  lista3  4  FaltouMemoria
=destruirlista lista3

== Limita qtd de memoria e tenta alocar, e depois elimina limites
=criarlista   lista4   0
=eliminarlimitesespaco

== Criar a lista0, anula sua cabeça e testa verificação
=criarlista      lista5   1
=deturparlista   lista5   DeturpaListaNulo
=verificarlista  lista5   ErroEstrutura
=AceitaFalhasAcumuladas 1

== Faz lista5 apontar para lixo
=deturparlista   lista5   DeturpaListaLixo
=verificarlista  lista5   ErroEstrutura
=AceitaFalhasAcumuladas 1

== Cria nova lista e deturpa tipo do espaco
=criarlista     lista6    1
=deturparlista  lista6    DeturpaEspacoLista
=verificarlista lista6    ErroEstrutura
=AceitaFalhasAcumuladas  1

== Cria uma nova lista e deturpa (caso vazio)
=criarlista    lista7  1

== Lista vazia não pode ter origem
=deturparlista  lista7  DeturpaOrigemLixo
=verificarlista lista7  ErroEstrutura
=AceitaFalhasAcumuladas  1

== Corrige origem e deturpa corrente
=deturparlista  lista7  DeturpaOrigemNulo
=deturparlista  lista7  DeturpaCorrenteLixo
=verificarlista lista7  ErroEstrutura
=AceitaFalhasAcumuladas  1

== Corrige corrente e deturpa final
=deturparlista  lista7  DeturpaCorrenteNulo
=deturparlista  lista7  DeturpaFimLixo
=verificarlista lista7  ErroEstrutura
=AceitaFalhasAcumuladas  1

== Cria lista nao vazia
=criarlista    lista8  1
=inselemapos   lista8  3  OK
=inselemapos   lista8  4  OK

== Primeiro, deturpa numero de elementos
=deturparlista   lista8  DeturpaNumElem
=verificarlista  lista8  ErroEstrutura
=AceitaFalhasAcumuladas  1


== Deturpa origem de lista não vazia
=deturparlista  lista8  DeturpaOrigemNulo
=verificarlista lista8  ErroEstrutura
=AceitaFalhasAcumuladas  1

== Corrige origem e deturpa corrente de lista não vazia
=deturparlista  lista8  DeturpaOrigemLixo
=deturparlista  lista8  DeturpaCorrenteNulo
=verificarlista lista8  ErroEstrutura
=AceitaFalhasAcumuladas  1

== Corrige corrente e deturpa fim de lista não vazia
=deturparlista  lista8  DeturpaCorrenteLixo
=deturparlista  lista8  DeturpaFimNulo
=verificarlista lista8  ErroEstrutura
=AceitaFalhasAcumuladas  1


== Cria lista nao vazia, para testar erro de encadeamento anterior
=criarlista    lista9  1
=inselemapos   lista9  5  OK
=inselemapos   lista9  2  OK
=irinicio      lista9

== Coloca lixo em origem e no fim a remove
=avancarelem    lista9  1  OK
=deturparlista  lista9  DeturpaPtAntPtProx
=verificarlista lista9  ErroEstrutura
=AceitaFalhasAcumuladas  1

== Agora, deturpa o tipo de espaco do anterior
=avancarelem    lista9  -1 OK
=deturparlista  lista9  DeturpaEspacoElemento
=verificarlista lista9  ErroEstrutura
=AceitaFalhasAcumuladas 1

== Cria lista nao vazia, para testar erro de encadeamento anterior
=criarlista    lista10  1
=inselemapos   lista10  5  OK
=inselemapos   lista10  2  OK
=irinicio      lista10

== Testar erro de encadeamento com seguinte
=deturparlista  lista10  DeturpaPtProxPtAnt
=verificarlista lista10  ErroEstrutura
=AceitaFalhasAcumuladas  1

== Testar ptProx nulo em elemento não final
=deturparlista  lista10  DeturpaPtProxNulo
=verificarlista lista10  ErroEstrutura
=AceitaFalhasAcumuladas  1

== Cria lista nao vazia, para testar erro de não apontar pra cabeça
=criarlista    lista11  1
=inselemapos   lista11  5  OK
=deturparlista lista11  DeturpaPtCabecaNulo
=verificarlista lista11 ErroEstrutura
=AceitaFalhasAcumuladas 1

== Com a mesma lista, torna nao integro a origem
=deturparlista lista11  DeturpaOrigemLixo
=verificarlista lista11 ErroEstrutura
=AceitaFalhasAcumuladas 1

== Cria lista nao vazia, para testar erro de não apontar pra cabeça
=criarlista    lista12  1
=inselemapos   lista12  5  OK
=deturparlista lista12  DeturpaCorrenteLixo
=verificarlista lista12 ErroEstrutura
=AceitaFalhasAcumuladas 1

== Por fim, testa anterior nulo em elemento não origem
=criarlista     lista13  1 
=inselemapos    lista13  3  OK
=inselemapos    lista13  6  OK
=deturparlista  lista13  DeturpaPtAntNulo 
=verificarlista lista13  ErroEstrutura
=AceitaFalhasAcumuladas 1

== Limpa a memória
=limparmemoria


== Terminar controlar contadores
=pararcontagem
=verificarcontagens    0

== Terminar contagem
=gravarcontadores "..\\scripts\\TesteListaDeturpa.estat"
=terminarcontadores

// fim
